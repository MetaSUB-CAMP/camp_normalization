'''Workflow for the CAMP Normalization module.'''


from contextlib import redirect_stderr
import os
from os.path import abspath, basename, dirname, join
import pandas as pd
import shutil
from utils import Workflow_Dirs, ingest_samples
from itertools import chain


# Load and/or make the working directory structure
dirs = Workflow_Dirs(config['work_dir'], 'normalization')


# Load sample names and input files 
SAMPLES = ingest_samples(config['samples'], dirs.TMP)


# Specify the location of any external resources and scripts
# dirs_ext = join(dirname(abspath(__file__)), 'ext')
dirs_ext = join(config['workflow_dir'], 'ext')
dirs_scr = join(dirs_ext, 'scripts')


METHODS_d = {'scaling' : ['tss', 'tmm'],
             'coda' : ['clr', 'alr', 'ilr'],
             'transformation' : ['blom', 'npn'],
             'batch' : ['combat', 'limma']}
METHODS_l = set()
if config['all']:
   METHODS_l.update(chain.from_iterable(METHODS_d.values()))
if config['scaling']:
   METHODS_l.update(METHODS_d['scaling'])
if config['coda']:
   METHODS_l.update(METHODS_d['coda'])
if config['transformation']:
   METHODS_l.update(METHODS_d['transformation'])
if config['batchcorr']:
   METHODS_l.update(METHODS_d['batchcorr'])
if config['fromthelist']:
   METHODS_l.update(config['methodslist'])

rule all:
    input:
        expand(join(dirs.OUT, '{sample}_{method}.csv'), sample=SAMPLES, method=METHODS_l)


# TODO: add log to rules

rule dataprep:
    input:
        ftable = join(dirs.TMP, '{sample}.csv')
    output:
        ftable_prepped = join(dirs.OUT, '{sample}_prepped.csv')
    conda:
        'normalization'
    params:
        data_prep_scr = join(dirs_scr, 'data-prep.R')
    shell:
        """
        Rscript --vanilla {params.data_prep_scr} {input.ftable} {output.ftable_prepped}
        """

rule normalize:
    input:
        ftable_prepped = join(dirs.OUT, '{sample}_prepped.csv'),
        metadata = join(dirs.TMP, '{sample}_metadata.csv')
    output:
        ftable_norm = join(dirs.OUT, '{sample}_{method}.csv')
    params:
        data_norm_scr = join(dirs_scr, 'normalizer.R')
    conda:
        'normalization'
    shell:
        """
        Rscript --vanilla {params.data_norm_scr} \
                -i {input.ftable_prepped} \
                -m {wildcards.method} \
                --meta {input.metadata} \
                -o {output.ftable_norm}
        """